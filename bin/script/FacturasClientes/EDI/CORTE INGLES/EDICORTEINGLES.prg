#include "Factu.ch" 
#include "FiveWin.ch"

#define __localDirectory__       "c:\eDiversa\send\Planos\"
//#define __localDirectory__       "c:\ficheros\"
#define __separator__            "|"

//---------------------------------------------------------------------------//
//---------------------------------------------------------------------------//
/*
INVOIC_D_93A_UN_EAN007
INV|1600711|380|9
DTM|20151209
PAI|60
RFF|DQ|1600710
RFF|ON|530957173
NADSU|8437003477959|MARISCOS MENDEZ  SL|R.M.Huelva,Tomo269 Gral.Lb.96 Secc.1º,Fol.96, Hoja H-1925 Inscr.1º|POLIGONO PESQUERO NORTE S/N|HUELVA|21002     |B21173786
NADSCO|8437003477959|MARISCOS MENDEZ  SL|R.M.Huelva,Tomo269 Gral.Lb.96 Secc.1º,Fol.96, Hoja H-1925 Inscr.1º|POLIGONO PESQUERO NORTE S/N|HUELVA|21002     |B21173786
NADBY|8480015009007|CENTROS COMERCIALES CARREFOUR, S.A.SEVIL|P. I. DE LA ISLA C/TORRE DE LOS HEBREROS|DOS HERMANAS|41700     |A28425270|
NADBCO|8480015009007|CENTROS COMERCIALES CARREFOUR, S.A.SEVIL|P. I. DE LA ISLA C/TORRE DE LOS HEBREROS|DOS HERMANAS|41700     |A28425270
NADII|8437003477959|MARISCOS MENDEZ  SL|POLIGONO PESQUERO NORTE S/N|HUELVA|21002     |B21173786
NADIV| 8480015111113            |CENTROS COMERCIALES CARREFOUR, S.A.SEVIL|EDIFICIO CARREFOUR C/ CAMPEZO Nº16|POL. INDUSTRIAL LAS MERCEDES|28022     |A28425270
NADPR|8480015111113
NADDP|8480015102036
CUX|EUR|4
LIN|2928771000006 |EN
IMDLIN|LANGT. TG. H COC 2Kg|M
QTYLIN|47|         10.000
MOALIN|        112.700
PRILIN|AAB|         11.270
PRILIN|AAA|         11.270
TAXLIN|VAT|         10.000
CNTRES|2
MOARES|        112.700|        112.700|        112.700|         123.97|          11.27|          0.000
TAXRES|VAT|         10.000|         11.270|        112.700



//---------------------------------------------------------------------------//
//---------------------------------------------------------------------------//
//---------------------------------------------------------------------------//
//---------------------------------------------------------------------------//
//---------------------------------------------------------------------------//


INVOIC_D_93A_UN_EAN007
INV|1600716|380|9
DTM|20151210
PAI|60
RFF|DQ|1600715
RFF|ON|530957287
NADSU|8437003477959|MARISCOS MENDEZ  SL|R.M.Huelva,Tomo269 Gral.Lb.96 Secc.1º,Fol.96, Hoja H-1925 Inscr.1º|POLIGONO PESQUERO NORTE S/N|HUELVA|21002     |B21173786
NADSCO|8437003477959|MARISCOS MENDEZ  SL|R.M.Huelva,Tomo269 Gral.Lb.96 Secc.1º,Fol.96, Hoja H-1925 Inscr.1º|POLIGONO PESQUERO NORTE S/N|HUELVA|21002     |B21173786
NADBY|8480015009007|CENTROS COMERCIALES CARREFOUR, S.A.SEVIL|P. I. DE LA ISLA C/TORRE DE LOS HEBREROS|DOS HERMANAS|41700     |A28425270|
NADBCO|8480015009007|CENTROS COMERCIALES CARREFOUR, S.A.SEVIL|P. I. DE LA ISLA C/TORRE DE LOS HEBREROS|DOS HERMANAS|41700     |A28425270
NADII|8437003477959|MARISCOS MENDEZ  SL|POLIGONO PESQUERO NORTE S/N|HUELVA|21002     |B21173786
NADIV|             |CENTROS COMERCIALES CARREFOUR, S.A.SEVIL|EDIFICIO CARREFOUR C/ CAMPEZO Nº16|POL. INDUSTRIAL LAS MERCEDES|28022     |A28425270
NADPR|8480015102036
NADDP|8480015102036
CUX|EUR|4
LIN|8437012714359 |EN
IMDLIN|LANGT. TG. MTO COC. 400 GR|M
QTYLIN|47|         12.000
MOALIN|         85.200
PRILIN|AAB|          7.100
PRILIN|AAA|          7.100
TAXLIN|VAT|         10.000
LIN|8437012714373 |EN
IMDLIN|CUERPO PEQ. COC 400 Gr|M
QTYLIN|47|          8.000
MOALIN|         40.800
PRILIN|AAB|          5.100
PRILIN|AAA|          5.100
TAXLIN|VAT|         10.000
LIN|8437012714328 |EN
IMDLIN|GAMBA TDA COC. 400GR.|M
QTYLIN|47|         24.000
MOALIN|        234.000
PRILIN|AAB|          9.750
PRILIN|AAA|          9.750
TAXLIN|VAT|         10.000
LIN|8437003477508 |EN
IMDLIN|T. BACALAO (BDJA 10UNID.)|M
QTYLIN|47|          6.000
MOALIN|         13.800
PRILIN|AAB|          2.300
PRILIN|AAA|          2.300
TAXLIN|VAT|         10.000
LIN|8437003477492 |EN
IMDLIN|T. GAMBAS  (BDJA 10UNID.)|M
QTYLIN|47|         18.000
MOALIN|         41.400
PRILIN|AAB|          2.300
PRILIN|AAA|          2.300
TAXLIN|VAT|         10.000
LIN|8437003477515 |EN
IMDLIN|T. CAMARONES (BDJA 6 UNID.)|M
QTYLIN|47|         18.000
MOALIN|         41.400
PRILIN|AAB|          2.300
PRILIN|AAA|          2.300
TAXLIN|VAT|         10.000
LIN|8437012714632 |EN
IMDLIN|BRILLANTES MEDIANO ( Caja 250 Gr)|M
QTYLIN|47|         40.000
MOALIN|        274.000
PRILIN|AAB|          6.850
PRILIN|AAA|          6.850
TAXLIN|VAT|         10.000
LIN|8437012714649 |EN
IMDLIN|GAMBA PLANCHA ( Caja 300 Gr )|M
QTYLIN|47|          8.000
MOALIN|         47.200
PRILIN|AAB|          5.900
PRILIN|AAA|          5.900
TAXLIN|VAT|         10.000
LIN|8437012714625 |EN
IMDLIN|LANGOSTINO TIGRE ( Caja  250 GR)|M
QTYLIN|47|          4.000
MOALIN|         19.800
PRILIN|AAB|          4.950
PRILIN|AAA|          4.950
TAXLIN|VAT|         10.000
LIN|8437012714311 |EN
IMDLIN|GAMBA MED. ROJA COC. 400 Gr.|M
QTYLIN|47|          4.000
MOALIN|         27.200
PRILIN|AAB|          6.800
PRILIN|AAA|          6.800
TAXLIN|VAT|         10.000
LIN|8437003477782 |EN
IMDLIN|CUERPO MED COC 500Gr|M
QTYLIN|47|          4.000
MOALIN|         31.600
PRILIN|AAB|          7.900
PRILIN|AAA|          7.900
TAXLIN|VAT|         10.000
CNTRES|2
MOARES|        856.400|        856.400|        856.400|         942.04|          85.64|          0.000
TAXRES|VAT|         10.000|         85.640|        856.400







*/
//---------------------------------------------------------------------------//
//---------------------------------------------------------------------------//
//---------------------------------------------------------------------------//

Function EDI_CafesyZymos( lNoExportados, oTree, nView )

   local oTEdiExporarFacturas

   oTEdiExporarFacturas          := TEdiExporarFacturas():New( lNoExportados, oTree, nView )
   oTEdiExporarFacturas:Run()

Return nil

//---------------------------------------------------------------------------//

CLASS TEdiExporarFacturas

   DATA nView

   DATA oTree

   DATA lNoExportados

   DATA cFileEDI
   DATA oFileEDI

   DATA cCodigoCliente

   DATA sTotalFactura

   DATA nNumeroLineas

   METHOD New( lNoExportados, oTree, nView )
   METHOD Run()

   METHOD isFacturaProcesada()
   METHOD infoFacturaEnProceso() INLINE   ( ::oTree:Select( ::oTree:Add( "Factura : " + D():FacturasClientesIdText( ::nView ) + " en proceso.", 1 ) ) )
   METHOD infoFacturaGenerada()  INLINE   ( ::oTree:Select( ::oTree:Add( "Factura : " + D():FacturasClientesIdText( ::nView ) + " generada en el fichero " + ::cFileEDI, 1 ) ) )

   METHOD createFile()
   METHOD closeFile()            INLINE   ( ::oFileEDI:Close() )
   METHOD isFile()               INLINE   ( file( ::cFileEDI ) )

   METHOD writeDatosFijos()                  INLINE   ( ::oFileEDI:add( "INVOIC_D_93A_UN_EAN007" ) )
   METHOD writeDatosGenerales()
   METHOD writeFechas()
   METHOD writeFormadePago()

   METHOD writeObservaciones()

   METHOD writeReferenciaAlbaran()
   METHOD writeReferenciaPedido()
   METHOD writeDatosProveedor()

   METHOD writeDatosComprador()
   METHOD writeDatosCompradorLegal()

   METHOD writeEmisorFactura()
   METHOD writeReceptorFactura()

   METHOD writeReceptorMercancia()
   METHOD writeEmisorPago()

   METHOD writeDivisa()

   METHOD writeMarcaCalculoTotales()         INLINE   ( ::oFileEDI:add( "CNTRES|" + AllTrim( Str( ::nNumeroLineas ) ) ) )

   METHOD writeResumenTotales()
   METHOD writeResumenPrimerImpuesto()
   METHOD writeResumenSegundoImpuesto()
   METHOD writeResumenTercerImpuesto()

   METHOD writeLineas()

   METHOD writeCabeceraLinea()
   METHOD writeDescripcionesLinea()
   METHOD writeCantidadLinea()
   METHOD writeTotalLinea()
   METHOD writePrecioBrutoUnitarioLinea()
   METHOD writePrecioNetoUnitarioLinea()
   METHOD writeImpuestosLinea()
   METHOD writeObservacionesLinea()
   METHOD writeImporteImpuestosLinea()

   METHOD getNumero( nNumero )   INLINE   ( transform( nNumero, "@R 99999999999.999" ) )
   METHOD getFecha( dFecha )     INLINE   ( dtos( dFecha ) )

   METHOD isLineaValida()        INLINE   ( lValLine( D():FacturasClientesLineas( ::nView ) ) .and. !( D():FacturasClientesLineas( ::nView ) )->lTotLin .and. nTotNFacCli() != 0 )
   METHOD isDescuentoValido()    INLINE   ( ( D():FacturasClientesLineas( ::nView ) )->nDto != 0 )

   METHOD setFacturaClienteGeneradaEDI()

END CLASS

//---------------------------------------------------------------------------//

METHOD New( lNoExportados, oTree, nView )

   ::lNoExportados            := lNoExportados
   ::oTree                    := oTree
   ::nView                    := nView
   ::nNumeroLineas            := 1

Return ( self )

//---------------------------------------------------------------------------//

METHOD Run()

   local oNode

   if ::isFacturaProcesada()
      Return ( self )
   end if

   ::infoFacturaEnProceso()

   ::sTotalFactura         := sTotFacCli()

   ::cCodigoCliente        := ( D():FacturasClientes( ::nView ) )->cCodCli

   ::createFile()

   if ::isFile()

      ::writeDatosFijos()
      ::writeDatosGenerales()
      ::writeFechas()
      ::writeFormadePago()

      ::writeObservaciones()

      ::writeReferenciaAlbaran()
      ::writeReferenciaPedido()
      ::writeDatosComprador()
      ::writeReceptorFactura()
      ::writeDatosCompradorLegal()
      ::writeDatosProveedor()
      ::writeReceptorMercancia()

      ::writeDivisa()

      ::writeLineas()
      ::writeMarcaCalculoTotales()
      
      ::writeResumenTotales()
      ::writeResumenPrimerImpuesto()
      ::writeResumenSegundoImpuesto()
      ::writeResumenTercerImpuesto()
      
      ::closeFile()

      ::setFacturaClienteGeneradaEDI()

      ::infoFacturaGenerada()

   end if

Return ( self )

//---------------------------------------------------------------------------//

METHOD isFacturaProcesada()

   if ( D():FacturasClientes( ::nView ) )->lExpEdi .and. ::lNoExportados
      ::oTree:Select( ::oTree:Add( "Factura : " + D():FacturasClientesIdText( ::nView ) + " anteriormente generada.", 1 ) )
      Return ( .t. )
   end if

Return ( .f. )

//---------------------------------------------------------------------------//

METHOD createFile()

   ::cFileEDI     := __localDirectory__ + "F" + D():FacturasClientesIdShort( ::nView ) + ".pla" 

   if file( ::cFileEDI )
      fErase( ::cFileEDI )
   end if

   ::oFileEDI     := TTxtFile():New( ::cFileEDI )

Return ( self )

//---------------------------------------------------------------------------//

METHOD writeDatosGenerales()

   local cLine    := "INV" + __separator__
   cLine          += D():FacturasClientesIdShort( ::nView ) + __separator__
   cLine          += "380" + __separator__
   cLine          += "9"

   ::oFileEDI:add( cLine )

Return ( self )

//---------------------------------------------------------------------------//

METHOD writeFechas()

   local cLine    := "DTM" + __separator__
   cLine          += ::getFecha( ( D():FacturasClientes( ::nView ) )->dFecFac )

   ::oFileEDI:add( cLine )

Return ( self )

//---------------------------------------------------------------------------//

METHOD writeFormadePago()

   local cLine    := "PAI" + __separator__
   cLine          += AllTrim( RetFld( ( D():FacturasClientes( ::nView ) )->cCodPago, D():FormasPago( ::nView ), "cCodEdi" ) )

   ::oFileEDI:add( cLine )

Return ( self )

//---------------------------------------------------------------------------//

METHOD writeObservaciones()

   local cLine    := "TXT" + __separator__
   cLine          += "AAI" + __separator__
   cLine          += AllTrim( ( D():FacturasClientes( ::nView ) )->mObserv )

   ::oFileEDI:add( cLine )

Return ( self )

//---------------------------------------------------------------------------//

METHOD writeReferenciaAlbaran()

   local cLine    := "RFF" + __separator__
   cLine          += "DQ" + __separator__
   cLine          += AllTrim( ( D():FacturasClientes( ::nView ) )->cSuAlb )

   ::oFileEDI:add( cLine )

Return ( self )

//---------------------------------------------------------------------------//

METHOD writeReferenciaPedido()

   local cLine    := "RFF" + __separator__
   cLine          += "ON" + __separator__
   cLine          += AllTrim( ( D():FacturasClientes( ::nView ) )->cSuFac )

   ::oFileEDI:add( cLine )

Return ( self )

//---------------------------------------------------------------------------//

METHOD writeDatosProveedor()

::oFileEDI:add( "NADSU|" + AllTrim( uFieldEmpresa( "cCodEdi" ) ) + "|MARISCOS MENDEZ  SL|R.M.Huelva,Tomo269 Gral.Lb.96 Secc.1,Fol.96, Hoja H-1925 Inscr.1|POLIGONO PESQUERO NORTE S/N|HUELVA|21002     |B21173786" )

::oFileEDI:add( "NADSCO|" + AllTrim( uFieldEmpresa( "cCodEdi" ) ) + "|MARISCOS MENDEZ  SL|R.M.Huelva,Tomo269 Gral.Lb.96 Secc.1,Fol.96, Hoja H-1925 Inscr.1|POLIGONO PESQUERO NORTE S/N|HUELVA|21002     |B21173786" )

Return ( self )

//---------------------------------------------------------------------------//

METHOD writeDatosComprador()

   local cCodigo
   local cLine    := "NADBY" + __separator__

   cCodigo        := getCustomExtraField( "025", "Clientes", ( D():FacturasClientes( ::nView ) )->cCodCli )

   if !Empty( cCodigo )
      cLine       += AllTrim( cCodigo ) + __separator__    //Codigo EDI
   else
      cLine       += "" + __separator__    //Codigo EDI
   end if

   cLine          += AllTrim( ( D():FacturasClientes( ::nView ) )->cNomCli ) + __separator__                                              //Nombre

   if ( D():Clientes( ::nView ) )->( dbSeek( ( D():FacturasClientes( ::nView ) )->cCodCli ) )
      cLine       += AllTrim( ( D():Clientes( ::nView ) )->cDomEnt ) + __separator__                                                      //Domicilio
      cLine       += AllTrim( ( D():Clientes( ::nView ) )->cPobEnt ) + __separator__                                                      //Poblacion
      cLine       += Padr( ( D():Clientes( ::nView ) )->cCPEnt, 10 ) + __separator__                                                      //Codigo Postal
   end if

   cLine          += AllTrim( ( D():FacturasClientes( ::nView ) )->cDniCli ) + __separator__                                              //NIF

   ::oFileEDI:add( cLine )

Return ( self )

//---------------------------------------------------------------------------//

METHOD writeDatosCompradorLegal()

   local cCodigo
   local cLine    := "NADBCO" + __separator__

   cCodigo        := AllTrim( getCustomExtraField( "025", "Clientes", ( D():FacturasClientes( ::nView ) )->cCodCli ) )    //Codigo EDI

   if !Empty( cCodigo )
      cLine       += AllTrim( cCodigo ) + __separator__    //Codigo EDI
   else
      cLine       += "" + __separator__    //Codigo EDI
   end if

   cLine          += AllTrim( ( D():FacturasClientes( ::nView ) )->cNomCli ) + __separator__                                              //Nombre

   if ( D():Clientes( ::nView ) )->( dbSeek( ( D():FacturasClientes( ::nView ) )->cCodCli ) )
      cLine       += AllTrim( ( D():Clientes( ::nView ) )->cDomEnt ) + __separator__                                                      //Domicilio
      cLine       += AllTrim( ( D():Clientes( ::nView ) )->cPobEnt ) + __separator__                                                      //Poblacion
      cLine       += Padr( ( D():Clientes( ::nView ) )->cCPEnt, 10 ) + __separator__                                                      //Codigo Postal
   end if

   cLine          += AllTrim( ( D():FacturasClientes( ::nView ) )->cDniCli ) + __separator__                                              //NIF

   ::oFileEDI:add( cLine )

Return ( self )

//---------------------------------------------------------------------------//

METHOD writeEmisorFactura()

   local cLine    := "NADII" + __separator__
   cLine          += AllTrim( uFieldEmpresa( "cCodEdi" ) ) + __separator__    //Codigo EDI
   cLine          += "MARISCOS MENDEZ  SL" + __separator__                    //Nombre
   cLine          += "POLIGONO PESQUERO NORTE S/N" + __separator__            //Domicilio
   cLine          += "HUELVA" + __separator__                                 //Población
   cLine          += "21002     " + __separator__                             //Código postal
   cLine          += "B21173786" + __separator__                              //CIF
   cLine          += "ES"                                                     //Pais

   ::oFileEDI:add( cLine )

Return ( self )

//---------------------------------------------------------------------------//

METHOD writeReceptorFactura()

   local cLine    := "NADIV" + __separator__
   cLine          += AllTrim( getCustomExtraField( "026", "Clientes", ( D():FacturasClientes( ::nView ) )->cCodCli ) ) + __separator__    //Codigo EDI
   cLine          += AllTrim( ( D():FacturasClientes( ::nView ) )->cNomCli ) + __separator__                                              //Nombre

   if ( D():Clientes( ::nView ) )->( dbSeek( ( D():FacturasClientes( ::nView ) )->cCodCli ) )
      cLine       += AllTrim( ( D():Clientes( ::nView ) )->Domicilio ) + __separator__                                                    //Domicilio
      cLine       += AllTrim( ( D():Clientes( ::nView ) )->Poblacion ) + __separator__                                                    //Poblacion
      cLine       += Padr( ( D():Clientes( ::nView ) )->CodPostal, 10 ) + __separator__                                                   //Codigo Postal
      cLine       += AllTrim( ( D():Clientes( ::nView ) )->Nif ) + __separator__                                                          //NIF
   end if

   ::oFileEDI:add( cLine )

Return ( self )

//---------------------------------------------------------------------------//

METHOD writeEmisorPago()

   local cLine    := "NADPR" + __separator__

   cLine          += AllTrim( getCustomExtraField( "026", "Clientes", ( D():FacturasClientes( ::nView ) )->cCodCli ) )            //Codigo EDI

   ::oFileEDI:add( cLine )

Return ( self )

//---------------------------------------------------------------------------//

METHOD writeReceptorMercancia()

   local cLine    := "NADDP" + __separator__

   if ( D():Clientes( ::nView ) )->( dbSeek( ( D():FacturasClientes( ::nView ) )->cCodCli ) )
      cLine       += AllTrim( ( D():Clientes( ::nView ) )->cCodEdi )                                                              //Codigo EDI   
   end if

   ::oFileEDI:add( cLine )

Return ( self )

//---------------------------------------------------------------------------//

METHOD writeDivisa()

   local cLine    := "CUX" + __separator__
   
   cLine          += "EUR" + __separator__   //Moneda
   cLine          += "4"                     //Calificador Moneda

   ::oFileEDI:add( cLine )

Return ( self )

//---------------------------------------------------------------------------//

METHOD writeResumenTotales()

   local cLine    := "MOARES" + __separator__
   cLine          += ::getNumero( ::sTotalFactura:nTotalBruto ) + __separator__
   cLine          += ::getNumero( ::sTotalFactura:nTotalNeto ) + __separator__
   cLine          += ::getNumero( ::sTotalFactura:TotalBase() ) + __separator__
   cLine          += ::getNumero( ::sTotalFactura:TotalDocumento() ) + __separator__
   cLine          += ::getNumero( ::sTotalFactura:TotalIva() ) + __separator__
   cLine          += ::getNumero( ::sTotalFactura:TotalDescuento() )

   ::oFileEDI:add( cLine )

Return ( self )

//---------------------------------------------------------------------------//

METHOD writeResumenPrimerImpuesto()
   
   local cLine    := ""

   if empty( ::sTotalFactura:nPorcentajePrimerIva() )
      Return ( self )
   end if 

   cLine          += "TAXRES" + __separator__
   cLine          += "VAT" + __separator__
   cLine          += ::getNumero( ::sTotalFactura:nPorcentajePrimerIva() ) + __separator__
   cLine          += ::getNumero( ::sTotalFactura:nTotalPrimerIva() ) + __separator__
   cLine          += ::getNumero( ::sTotalFactura:nBasePrimerIva() ) 

   ::oFileEDI:add( cLine )

Return ( self )

//---------------------------------------------------------------------------//

METHOD writeResumenSegundoImpuesto()
   
   local cLine    := ""

   if empty( ::sTotalFactura:nPorcentajeSegundoIva() )
      Return ( self )
   end if 

   cLine          += "TAXRES" + __separator__
   cLine          += "VAT" + __separator__
   cLine          += ::getNumero( ::sTotalFactura:nPorcentajeSegundoIva() ) + __separator__
   cLine          += ::getNumero( ::sTotalFactura:nTotalSegundoIva() ) + __separator__
   cLine          += ::getNumero( ::sTotalFactura:nBaseSegundoIva() ) 

   ::oFileEDI:add( cLine )

Return ( self )

//---------------------------------------------------------------------------//

METHOD writeResumenTercerImpuesto()

   local cLine    := ""

   if empty( ::sTotalFactura:nPorcentajeTercerIva() )
      Return ( self )
   end if 

   cLine          += "TAXRES" + __separator__
   cLine          += "VAT" + __separator__
   cLine          += ::getNumero( ::sTotalFactura:nPorcentajeTercerIva() ) + __separator__
   cLine          += ::getNumero( ::sTotalFactura:nTotalTercerIva() ) + __separator__
   cLine          += ::getNumero( ::sTotalFactura:nBaseTercerIva() ) 

   ::oFileEDI:add( cLine )

Return ( self )

//---------------------------------------------------------------------------//

METHOD writeLineas()

   local id       := D():FacturasClientesId( ::nView )

   if ( D():FacturasClientesLineas( ::nView ) )->( dbSeek( id ) )  

      while ( D():FacturasClientesLineasId( ::nView ) == id ) .and. !( D():FacturasClientesLineas( ::nView ) )->( eof() ) 

         if ::isLineaValida()

            if ( D():Articulos( ::nView ) )->( dbSeek( ( D():FacturasClientesLineas( ::nView ) )->cRef ) )

               ::writeCabeceraLinea()
               ::writeDescripcionesLinea()
               ::writeCantidadLinea()
               ::writeObservacionesLinea()
               ::writeTotalLinea()
               ::writePrecioBrutoUnitarioLinea()
               ::writeImpuestosLinea()
               ::writeImporteImpuestosLinea()

               ::nNumeroLineas++

            end if

         end if 
      
         ( D():FacturasClientesLineas( ::nView ) )->( dbSkip() ) 
      
      end while

   end if 
   
Return ( self )

//---------------------------------------------------------------------------//

METHOD writeCabeceraLinea()

   local cLine    := ""

   cLine          += "LIN" + __separator__
   cLine          += AllTrim( ( D():Articulos( ::nView ) )->cCodEdi ) + __separator__
   cLine          += "EN" 

   ::oFileEDI:add( cLine )

   cLine          := "PIALIN" + __separator__
   cLine          += AllTrim( ( D():Articulos( ::nView ) )->cCodEdi ) + __separator__
   cLine          += "EN" 

   ::oFileEDI:add( cLine )   

Return ( self )

//---------------------------------------------------------------------------//

METHOD writeDescripcionesLinea()

   local cLine    := ""

   cLine          += "IMDLIN" + __separator__
   cLine          += AllTrim( ( D():FacturasClientesLineas( ::nView ) )->cDetalle ) + __separator__
   cLine          += "M" 

   ::oFileEDI:add( cLine )

Return ( self )

//---------------------------------------------------------------------------//

METHOD writeCantidadLinea()

   local cLine    := ""

   cLine          += "QTYLIN" + __separator__
   cLine          += "47" + __separator__
   cLine          += ::getNumero( nTotNFacCli() )

   ::oFileEDI:add( cLine )

Return ( self )

//---------------------------------------------------------------------------//

METHOD writeObservacionesLinea()

   local cLine    := ""

   cLine          += "TXT" + __separator__
   cLine          += AllTrim( ( D():FacturasClientesLineas( ::nView ) )->mObsLin ) + __separator__
   cLine          += "M" 

   ::oFileEDI:add( cLine )

Return ( self )

//---------------------------------------------------------------------------//

METHOD writeTotalLinea()

   local cLine    := ""

   cLine          += "MOALIN" + __separator__
   cLine          += ::getNumero( nTotLFacCli() ) + __separator__
   cLine          += "66"

   ::oFileEDI:add( cLine )

Return ( self )

//---------------------------------------------------------------------------//

METHOD writePrecioBrutoUnitarioLinea()

   local cLine    := ""

   cLine          += "PRILIN" + __separator__
   cLine          += "AAB" + __separator__
   cLine          += ::getNumero( nTotUFacCli() )

   ::oFileEDI:add( cLine )

Return ( self )

//---------------------------------------------------------------------------//

METHOD writePrecioNetoUnitarioLinea()

   local cLine    := ""

   cLine          += "PRILIN" + __separator__
   cLine          += "AAA" + __separator__
   cLine          += ::getNumero( nTotUFacCli() )

   ::oFileEDI:add( cLine )

Return ( self )

//---------------------------------------------------------------------------//

METHOD writeImpuestosLinea()

local cLine    := ""

   cLine          += "TAXLIN" + __separator__
   cLine          += "VAT" + __separator__
   cLine          += ::getNumero( ( D():FacturasClientesLineas( ::nView ) )->nIva )

   ::oFileEDI:add( cLine )

Return ( self )

//---------------------------------------------------------------------------//

METHOD writeImporteImpuestosLinea()

   local cLine    := ""

   cLine          += "MOALIN" + __separator__
   cLine          += ::getNumero( nIvaLFacCli( D():FacturasClientesLineas( ::nView ) ) ) + __separator__
   cLine          += "124"

   ::oFileEDI:add( cLine )

Return ( self )

//---------------------------------------------------------------------------//

METHOD setFacturaClienteGeneradaEDI()

   if D():lockFacturasClientes( ::nView )
      ( D():FacturasClientes( ::nView ) )->lExpEdi    := .t.
      ( D():FacturasClientes( ::nView ) )->dFecEdi    := getSysDate()
      ( D():FacturasClientes( ::nView ) )->cHorEdi    := time()
      D():unlockFacturasClientes( ::nView )
   end if 

Return ( self )

//---------------------------------------------------------------------------//